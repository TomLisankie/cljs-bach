; A synthesiser we can play our piece on.
(defn synth [note]
  (connect->
    (add (square (* 1.01 (:pitch note))) (sawtooth (:pitch note)))
    (low-pass 600)
    (adsr 0.001 0.4 0.5 0.1)
    (gain 0.15)))

(def melody
       ; The durations and pitches of the notes. Try changing them.
  (->> (phrase [1 1/2 1/2 1 1 2 2] [0 1 0 2 -3 1 -1])
       (all :instrument synth))) ; Here we choose the instrument.

(def harmony
       ; The durations and pitches of the harmony's notes.
  (->> (phrase [1 1/2 1/2 1 1 1/2 1/2 1/2 1/2 2] [4 4 5 4 7 6 7 6 5 4])
       (all :instrument synth))) ; bell, wah, organ and marimba are also instruments.

(def bass
       ; The durations and pitches of the bass.
  (->> (phrase (cycle [3 0.75 0.25]) [-14 -15 -16 -17 -16 -15])
       (all :instrument wah)))

(def variation
       ; The durations and pitches of the bass.
  (->> (phrase [3 1 3/2 5/2] [9 11 8 10])
       (all :instrument marimba)))

(def beat
  (->> (phrase (repeat 8 1) (repeat 0))
       (all :instrument kick)))

(->> melody
     ;(with harmony)   ; Uncomment to include the harmony.
     ;(with bass)      ; Uncomment to include the bass.
     ;(with variation) ; Uncomment to include the harmony.
     ;(with beat)      ; Uncomment to include the beat.
     (tempo (bpm 90))  ; Try changing the number.
     (where :pitch (comp C major))) ; Try D or minor.
